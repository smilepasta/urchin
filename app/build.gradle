apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("yyyyMMdd", TimeZone.getTimeZone("UTC"))
}

def keystorePropertiesFile = rootProject.file("/app/keystore.properties")

def keystoreProperties = new Properties()

keystoreProperties.load(new FileInputStream(keystorePropertiesFile))

android {
    compileSdkVersion COMPILE_SDK_VERSION
    defaultConfig {
        applicationId "com.smilepasta.urchin"
        minSdkVersion MIN_SDK_VERSION
        targetSdkVersion TARGET_SDK_VERSION
        versionCode APP_VERSION_CODE
        versionName APP_VERSION_NAME
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    signingConfigs {
        debug {

        }
        release {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            zipAlignEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.debug
        }
        release {
            //是否混淆
            minifyEnabled true
            //Zipalign优化
            zipAlignEnabled true
            //移除无用的resource文件
            shrinkResources true
            //混淆文件
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            //签名
            signingConfig signingConfigs.release

            //gradle 3.0后使用这行代码
            android.applicationVariants.all { variant ->
                variant.outputs.all {
                    if (variant.buildType.name.equals('release')) {
                        outputFileName = "urchin_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}_release.apk"
                    } else {
                        outputFileName = "urchin_v${defaultConfig.versionName}_${defaultConfig.versionCode}_${releaseTime()}_debug.apk"
                    }
                }
            }
        }
    }

    flavorDimensions "smart"

    productFlavors {
        //release
        production {
            buildConfigField 'Boolean', 'IS_DEBUG', 'false'
            applicationId 'com.smilepasta.urchin'
            manifestPlaceholders = [
                    APP_NAME: "@string/app_name",
            ]
            dimension "smart"
        }
        //test
        dev {
            buildConfigField 'Boolean', 'IS_DEBUG', 'true'
            applicationId 'com.smilepasta.urchin.dev'
            manifestPlaceholders = [
                    APP_NAME: "@string/app_name_test",
            ]
            dimension "smart"
        }
        //VIP
    }

    compileOptions {
        targetCompatibility JAVA_VERSION
        sourceCompatibility JAVA_VERSION
    }

    lintOptions {
        disable 'MissingTranslation'
        ignoreWarnings true
    }
}



dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])

    implementation 'com.android.support:design:' + SUPPORT_LIB_VERSION
    implementation 'com.android.support:appcompat-v7:' + SUPPORT_LIB_VERSION
    implementation 'com.android.support:cardview-v7:' + SUPPORT_LIB_VERSION

    implementation 'com.squareup.retrofit2:retrofit:' + RETROFIT2_VERSION
    implementation 'com.squareup.retrofit2:converter-gson:' + RETROFIT2_VERSION
    implementation 'com.squareup.retrofit2:adapter-rxjava:' + RETROFIT2_VERSION
    implementation 'com.google.code.gson:gson:2.8.2'
    implementation 'io.reactivex:rxjava:1.3.8'
    implementation 'com.trello:rxlifecycle:' + RXLIFECYCLE_VERSION
    implementation 'com.trello:rxlifecycle-components:' + RXLIFECYCLE_VERSION

    implementation 'com.github.bumptech.glide:glide:3.7.0'
    implementation 'com.google.protobuf:protobuf-java:3.6.1'

    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    implementation 'com.android.support:support-v4:27.1.1'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'
}
